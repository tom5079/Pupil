apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-parcelize"
apply plugin: "kotlinx-serialization"
apply plugin: "com.google.android.gms.oss-licenses-plugin"

if (file("google-services.json").exists()) {
    logger.lifecycle("Firebase Enabled")
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
    apply plugin: "com.google.firebase.firebase-perf"
} else {
    logger.lifecycle("Firebase Disabled")
}

ext {
    okhttp_version = "3.12.12"
}

configurations {
    all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == "com.squareup.okhttp3" && details.requested.name == "okhttp") {
                    // OkHttp drops support before 5.0 since 3.13.0
                    details.useVersion okhttp_version
                }
            }
        }
    }
}

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "xyz.quaver.pupil"
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 69
        versionName "5.2.24"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            defaultConfig.minSdkVersion 21

            minifyEnabled false
            shrinkResources false

            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"

            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    buildFeatures {
        viewBinding true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xuse-experimental=kotlin.Experimental"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"
    
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.fragment:fragment-ktx:1.4.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"
    implementation "androidx.gridlayout:gridlayout:1.0.0"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation "androidx.work:work-runtime-ktx:2.7.1"
    implementation 'androidx.webkit:webkit:1.4.0'

    implementation "com.daimajia.swipelayout:library:1.2.0@aar"

    implementation "com.google.android.material:material:1.5.0"

    implementation platform('com.google.firebase:firebase-bom:29.0.3')
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-perf-ktx"

    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation "com.google.android.gms:play-services-mlkit-face-detection:17.0.0"

    implementation "com.github.clans:fab:1.6.4"

    //implementation "com.quiph.ui:recyclerviewfastscroller:0.2.1"

    implementation 'com.github.piasy:BigImageViewer:1.8.1'
    implementation 'com.github.piasy:FrescoImageLoader:1.8.1'
    implementation 'com.github.piasy:FrescoImageViewFactory:1.8.1'
    implementation 'com.facebook.fresco:imagepipeline-okhttp3:2.6.0'

    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"

    implementation "com.tbuonomo.andrui:viewpagerdotsindicator:4.1.2"

    implementation "net.rdrei.android.dirchooser:library:3.2@aar"
    implementation "com.gu:option:1.3"

    implementation "com.andrognito.patternlockview:patternlockview:1.0.0"
    //implementation "com.andrognito.pinlockview:pinlockview:2.1.0"

    implementation "ru.noties.markwon:core:3.1.0"

    implementation "org.jsoup:jsoup:1.14.3"

    implementation "xyz.quaver:documentfilex:0.7.2"
    implementation "xyz.quaver:floatingsearchview:1.1.7"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.test:runner:1.4.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}